apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'AndResGuard'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath ('com.tencent.mm:AndResGuard-gradle-plugin:1.2.0') { changing=true }
    }
}

ext {
    VERSION_DATE = new Date().format("yy-MM-dd", TimeZone.getTimeZone("UTC"))
    BUILD_TOOLS = "25.0.2"
}

android {
    signingConfigs {
        release{
            Properties properties = new Properties()
            InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream() ;
            properties.load( inputStream )

            storeFile file(properties["storeFile"])
            storePassword properties["storePassword"]
            keyAlias properties["keyAlias"]
            keyPassword properties["keyPassword"]
            v2SigningEnabled = false
        }
    }
    compileSdkVersion 25
    buildToolsVersion BUILD_TOOLS

    defaultConfig {
        applicationId "ml.puredark.hviewer"
        resValue "string", "app_name", "H-Viewer"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 85
        versionName "0.11.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        //multiDexEnabled true

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            resValue "string", "app_name", "H-Viewer-debug"
            ndk {
                abiFilters 'armeabi', 'armeabi-v7a' , 'x86'
            }
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-fresco.pro'
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-fresco.pro'
            zipAlignEnabled true
            signingConfig signingConfigs.release
            ndk {
                abiFilters 'armeabi', 'armeabi-v7a'
            }
        }
    }
    lintOptions {
        abortOnError false
    }
    productFlavors {
        coolapk {}
        github {}
        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'lib/*/libijk*.so'
        exclude 'lib/*/libRSSupport.so'
        exclude 'lib/*/librsjni.so'
    }
}

andResGuard {
    use7zip = true
    useSign = true
    keepRoot = false
    whiteList = [
            "R.mipmap.ic_launcher",
            "R.id.*"
    ]
    compressFilePattern = [
            "*.png",
            "*.jpg",
            "*.jpeg",
            "*.gif",
            "resources.arsc"
    ]
    sevenzip {
        artifact = 'com.tencent.mm:SevenZip:1.2.0'
    }
}


repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "http://kodbiro.github.io/repo/" }
    maven { url "http://dl.bintray.com/glomadrian/maven" }
    maven { url 'http://guardian.github.com/maven/repo-releases' }
    maven { url "https://raw.github.com/laenger/maven-releases/master/releases" }
    jcenter()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    annotationProcessor 'com.google.dagger:dagger-compiler:2.0'
    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    // Google offical
    // warning due to a known bug, ignored
    compile 'com.android.support:support-v4:25.2.0'
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:cardview-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    // umeng analytics
    compile 'com.umeng.analytics:analytics:latest.integration'
    // material widget
    compile 'com.github.vajro:MaterialDesignLibrary:1.6'
    compile 'com.github.opacapp:multiline-collapsingtoolbar:v1.3.1'
    compile 'com.github.rengwuxian:MaterialEditText:2.1.4'
    compile 'com.github.clans:fab:1.6.1'
    compile 'com.balysv:material-ripple:1.0.2'
    compile 'com.github.MiguelCatalan:MaterialSearchView:v1.4.0'
    compile 'com.github.glomadrian:MaterialAnimatedSwitch:1.1@aar'
    compile('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.10.4@aar') {
        transitive = true
    }
    compile 'com.github.leonardarnold:NumberPickerPreference:bea5996cc9'
    compile 'me.majiajie:pager-bottom-tab-strip:1.0.0'
    compile 'com.github.davidpizarro:autolabelui:1.0.1'
    compile('com.github.smtchahal:MaterialSpinner:1.1.7') {
        exclude group: 'com.nineoldandroids', module: 'library'
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    compile 'biz.laenger.android:vpbs:0.0.4'
    // Pull Load More
    compile('com.wuxiaolong.pullloadmorerecyclerview:library:1.1.2') {
        exclude group: 'com.android.support'
    }
    // SwipeBack

    //    compile 'me.majiajie:swipe-back:1.0.0-alpha2'

    // butter knife
    compile 'com.jakewharton:butterknife:8.2.1'
    // image
    compile 'com.facebook.fresco:fresco:0.13.0'
    compile 'com.facebook.fresco:animated-gif:0.13.0'
    compile 'com.facebook.fresco:imagepipeline-okhttp3:0.12.0+'
    compile 'q.rorbin:FastImageSize:1.0.2'
    //    compile 'me.relex:photodraweeview:1.1.0'
    compile 'com.github.PureDark:PhotoDraweeView:2c7b7f65b1'
    //    compile project (':photodraweeview')

    // animation
    compile 'com.daimajia.easing:library:2.0@aar'
    compile 'com.daimajia.androidanimations:library:2.2@aar'
    // http
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.lzy.net:okgo:2.1.4'
    compile 'com.lzy.net:okserver:1.1.3'
    // Stetho core
    compile 'com.facebook.stetho:stetho:1.4.2'
    //If you want to add a network helper
    compile 'com.facebook.stetho:stetho-okhttp3:1.4.2'
    // httpdns
    compile project(':httpdns')
    // email
    compile files('libs/activation.jar')
    compile files('libs/additionnal.jar')
    compile files('libs/mail.jar')
    compile files('libs/commons-email-1.4.jar')
    // format, gson, jsoup, etc
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.github.jhy:jsoup:jsoup-1.9.2'
    compile('com.jayway.jsonpath:json-path:2.2.0') {
        exclude group: 'org.ow2.asm'
    }
    // zxing
    compile 'com.journeyapps:zxing-android-embedded:3.0.2@aar'
    compile 'com.google.zxing:core:3.2.0'
    // directory chooser
    compile 'com.gu:option:1.3'
    compile 'net.rdrei.android.dirchooser:library:3.2@aar'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.2.1'
    compile 'net.jcip:jcip-annotations:1.0'
    // video player
    //compile 'com.github.CarGuo:GSYVideoPlayer:v1.6.1'
    compile 'com.github.PureDark:GSYVideoPlayer:ddcf05191e'

    // unlock methods, such as pin, fingerprint, etc
    compile 'com.andrognito.pinlockview:pinlockview:2.0.1'
    compile 'com.zwh:RxFingerPrinter:1.0.1'
    compile 'me.zhanghai.android.patternlock:library:2.0.4'
    //compile 'com.eightbitlab:blurview:1.3.2'
    compile 'com.github.PureDark:BlurView:e26c60d30e'
    //mutidex
    compile 'com.android.support:multidex:+'
}
